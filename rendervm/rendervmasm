#!/usr/bin/env perl

use strict;
use warnings;
use Getopt::Long;
use Data::Dumper;
my $opts = {};
GetOptions(
    $opts,
    'input=s',
    'output=s',
    'carray',
    'cenum',
    'cswitch',
    'copcode2str',
    'ctests',
);

my %INS_ARG_LENGTH = (
    HALT                => [0x00, [0x00]],
    YIELD               => [0x01, [0x00]],
    RESET               => [0x02, [0x00]],
    CALL                => [0x03, [0x02]],
    RETURN              => [0x04, [0x00]],
    JUMP                => [0x05, [0x02]],
    UINT8_POP           => [0x06, [0x02]],
    UINT8_DUP           => [0x07, [0x02]],
    UINT8_SWAP          => [0x08, [0x00]],
    UINT8_JUMPEM        => [0x09, [0x02]],
    UINT8_STORE         => [0x0a, [0x00]],
    UINT8_LOAD          => [0x0b, [0x00]],
    UINT8_ADD           => [0x0c, [0x00]],
    UINT8_SUB           => [0x0d, [0x00]],
    UINT8_MUL           => [0x0e, [0x00]],
    UINT8_EQ            => [0x0f, [0x00]],
    UINT8_JUMPNZ        => [0x10, [0x02]],
    UINT8_JUMPZ         => [0x11, [0x02]],
    UINT8_PUSH          => [0x12, [0x01]],
    UINT16_POP          => [0x13, [0x02]],
    UINT16_DUP          => [0x14, [0x02]],
    UINT16_SWAP         => [0x15, [0x00]],
    UINT16_JUMPEM       => [0x16, [0x02]],
    UINT16_STORE        => [0x17, [0x00]],
    UINT16_LOAD         => [0x18, [0x00]],
    UINT16_ADD          => [0x19, [0x00]],
    UINT16_SUB          => [0x1a, [0x00]],
    UINT16_MUL          => [0x1b, [0x00]],
    UINT16_EQ           => [0x1c, [0x00]],
    UINT16_JUMPNZ       => [0x1d, [0x02]],
    UINT16_JUMPZ        => [0x1e, [0x02]],
    UINT16_PUSH         => [0x1f, [0x02]],
    UINT32_POP          => [0x20, [0x02]],
    UINT32_DUP          => [0x21, [0x02]],
    UINT32_SWAP         => [0x22, [0x00]],
    UINT32_JUMPEM       => [0x23, [0x02]],
    UINT32_STORE        => [0x24, [0x00]],
    UINT32_LOAD         => [0x25, [0x00]],
    UINT32_ADD          => [0x26, [0x00]],
    UINT32_SUB          => [0x27, [0x00]],
    UINT32_MUL          => [0x28, [0x00]],
    UINT32_EQ           => [0x29, [0x00]],
    UINT32_JUMPNZ       => [0x2a, [0x02]],
    UINT32_JUMPZ        => [0x2b, [0x02]],
    UINT32_PUSH         => [0x2c, [0x04]],
    FLOAT_POP           => [0x2d, [0x02]],
    FLOAT_DUP           => [0x2e, [0x02]],
    FLOAT_SWAP          => [0x2f, [0x00]],
    FLOAT_JUMPEM        => [0x30, [0x02]],
    FLOAT_STORE         => [0x31, [0x00]],
    FLOAT_LOAD          => [0x32, [0x00]],
    FLOAT_ADD           => [0x33, [0x00]],
    FLOAT_SUB           => [0x34, [0x00]],
    FLOAT_MUL           => [0x35, [0x00]],
    FLOAT_EQ            => [0x36, [0x00]],
    FLOAT_JUMPNZ        => [0x37, [0x02]],
    FLOAT_JUMPZ         => [0x38, [0x02]],
    FLOAT_PUSH          => [0x39, [0x04]],
    VEC2_POP            => [0x3a, [0x02]],
    VEC2_DUP            => [0x3b, [0x02]],
    VEC2_SWAP           => [0x3c, [0x00]],
    VEC2_JUMPEM         => [0x3d, [0x02]],
    VEC2_STORE          => [0x3e, [0x00]],
    VEC2_LOAD           => [0x3f, [0x00]],
    VEC2_ADD            => [0x40, [0x00]],
    VEC2_SUB            => [0x41, [0x00]],
    VEC2_MUL            => [0x42, [0x00]],
    VEC2_EQ             => [0x43, [0x00]],
    VEC2_EXPLODE        => [0x44, [0x00]],
    VEC2_IMPLODE        => [0x45, [0x00]],
    VEC2_MULMAT2        => [0x46, [0x00]],
    VEC2_MULMAT3        => [0x47, [0x00]],
    VEC2_MULMAT4        => [0x48, [0x00]],
    VEC3_POP            => [0x49, [0x02]],
    VEC3_DUP            => [0x4a, [0x02]],
    VEC3_SWAP           => [0x4b, [0x00]],
    VEC3_JUMPEM         => [0x4c, [0x02]],
    VEC3_STORE          => [0x4d, [0x00]],
    VEC3_LOAD           => [0x4e, [0x00]],
    VEC3_ADD            => [0x4f, [0x00]],
    VEC3_SUB            => [0x50, [0x00]],
    VEC3_MUL            => [0x51, [0x00]],
    VEC3_EQ             => [0x52, [0x00]],
    VEC3_EXPLODE        => [0x53, [0x00]],
    VEC3_IMPLODE        => [0x54, [0x00]],
    VEC3_MULMAT3        => [0x55, [0x00]],
    VEC3_MULMAT4        => [0x56, [0x00]],
    VEC4_POP            => [0x57, [0x02]],
    VEC4_DUP            => [0x58, [0x02]],
    VEC4_SWAP           => [0x59, [0x00]],
    VEC4_JUMPEM         => [0x5a, [0x02]],
    VEC4_STORE          => [0x5b, [0x00]],
    VEC4_LOAD           => [0x5c, [0x00]],
    VEC4_ADD            => [0x5d, [0x00]],
    VEC4_SUB            => [0x5e, [0x00]],
    VEC4_MUL            => [0x5f, [0x00]],
    VEC4_EQ             => [0x60, [0x00]],
    VEC4_EXPLODE        => [0x61, [0x00]],
    VEC4_IMPLODE        => [0x62, [0x00]],
    VEC4_MULMAT4        => [0x63, [0x00]],
    MAT2_POP            => [0x64, [0x02]],
    MAT2_DUP            => [0x65, [0x02]],
    MAT2_SWAP           => [0x66, [0x00]],
    MAT2_JUMPEM         => [0x67, [0x02]],
    MAT2_STORE          => [0x68, [0x00]],
    MAT2_LOAD           => [0x69, [0x00]],
    MAT2_ADD            => [0x6a, [0x00]],
    MAT2_SUB            => [0x6b, [0x00]],
    MAT2_MUL            => [0x6c, [0x00]],
    MAT2_EQ             => [0x6d, [0x00]],
    MAT2_EXPLODE        => [0x6e, [0x00]],
    MAT2_IDENT          => [0x6f, [0x00]],
    MAT2_IMPLODE        => [0x70, [0x00]],
    MAT2_ROTATE         => [0x71, [0x00]],
    MAT2_SCALE          => [0x72, [0x00]],
    MAT2_TRANSP         => [0x73, [0x00]],
    MAT3_POP            => [0x74, [0x02]],
    MAT3_DUP            => [0x75, [0x02]],
    MAT3_SWAP           => [0x76, [0x00]],
    MAT3_JUMPEM         => [0x77, [0x02]],
    MAT3_STORE          => [0x78, [0x00]],
    MAT3_LOAD           => [0x79, [0x00]],
    MAT3_ADD            => [0x7a, [0x00]],
    MAT3_SUB            => [0x7b, [0x00]],
    MAT3_MUL            => [0x7c, [0x00]],
    MAT3_EQ             => [0x7d, [0x00]],
    MAT3_EXPLODE        => [0x7e, [0x00]],
    MAT3_IDENT          => [0x7f, [0x00]],
    MAT3_IMPLODE        => [0x80, [0x00]],
    MAT3_ROTATE         => [0x81, [0x00]],
    MAT3_SCALE          => [0x82, [0x00]],
    MAT3_TRANSL         => [0x83, [0x00]],
    MAT3_TRANSP         => [0x84, [0x00]],
    MAT4_POP            => [0x85, [0x02]],
    MAT4_DUP            => [0x86, [0x02]],
    MAT4_SWAP           => [0x87, [0x00]],
    MAT4_JUMPEM         => [0x88, [0x02]],
    MAT4_STORE          => [0x89, [0x00]],
    MAT4_LOAD           => [0x8a, [0x00]],
    MAT4_ADD            => [0x8b, [0x00]],
    MAT4_SUB            => [0x8c, [0x00]],
    MAT4_MUL            => [0x8d, [0x00]],
    MAT4_EQ             => [0x8e, [0x00]],
    MAT4_EXPLODE        => [0x8f, [0x00]],
    MAT4_IDENT          => [0x90, [0x00]],
    MAT4_IMPLODE        => [0x91, [0x00]],
    MAT4_ROTATEX        => [0x92, [0x00]],
    MAT4_ROTATEY        => [0x93, [0x00]],
    MAT4_ROTATEZ        => [0x94, [0x00]],
    MAT4_SCALE          => [0x95, [0x00]],
    MAT4_TRANSL         => [0x96, [0x00]],
    MAT4_TRANSP         => [0x97, [0x00]],
);

exit main($opts);

sub sorted_keys {
    return sort {$INS_ARG_LENGTH{$a}->[0] <=> $INS_ARG_LENGTH{$b}->[0]} keys %INS_ARG_LENGTH;
}

sub print_ctests {
    for my $ins (sorted_keys()) {
        printf("void test_opcode_$ins(test_harness_t* test, rendervm_t* vm) {\n");
        printf("    uint8_t program[] = {VM_$ins};\n");
        printf("    rendervm_exec(vm, program, 1);\n");
        printf("    rendervm_reset(vm);\n");
        printf("}\n\n");
    }
    printf("void test_all_opcodes(test_harness_t* test, rendervm_t* vm) {\n");
    for my $ins (sorted_keys()) {
        printf("test_opcode_$ins(test, vm);\n");
    }
    printf("}\n");
}

=item
=cut

sub print_copcode2str {
    my @array;
    for my $ins (sorted_keys()) {
        my $idx = $INS_ARG_LENGTH{$ins}->[0];
        $array[$idx] = $ins;
    }

    print "const char *opcode2str[] = {\n";
    for my $ins (@array) {
        if (defined $ins) {
            print "    \"$ins\",\n";
        }
        else {
            print "    \"[unused]\",\n";
        }
    }
    print "    \"[unused]\"\n";
    print "};\n";
}

sub print_cenum {
    print "typedef enum rendervm_opcode {\n";
    for my $ins (sorted_keys()) {
        my $opcode = $INS_ARG_LENGTH{$ins}->[0];
        printf("    VM_$ins = 0x%02x,\n", $opcode);
    }
    print "    VM__INSEND = 0xff\n";
    print "} rendervm_opcode_t;\n";
}

sub print_cswitch {
    print "    switch (opcode) {\n";
    for my $ins (sorted_keys()) {
        print "        case $ins:\n";
        print "            printf(\"$ins: UNIMPLEMENTED\");\n";
        print "            vm->running = 0;\n";
        print "            break;\n";
    }
    print "        default:\n";
    print "            break;\n";
    print "    }\n";
}

sub print_carray {
    my (@binary) = @_;

    my $len = scalar(@binary);
    print "    uint8_t program[$len] = {\n";
    print "        ";
    my $c = 1;
    while (scalar(@binary)) {
        my $int = shift @binary;
        printf("0x%02x", $int);
        if ($c != $len) {
            print ",";
        }
        else {
            print "\n";
        }
        if (!($c % 8)) {
            print "\n        ";
        }
        else {
            print " ";
        }
        $c++;
    }
    print "   };\n";
}

sub write_binary {
    my ($file, @binary) = @_;
    my $bin = pack("C*", @binary);
    open(my $ofh, '>:raw', $file) || die "$!";
    print $ofh $bin;
    close($ofh);
}

sub _expand_constant {
    my ($val, $len) = @_;

    my @codes;
    for (my $i = 0; $i < $len; $i++) {
        push @codes, $val & 255;
        $val = $val >> 8;
    }

    return @codes;
}

sub _convert_to_dec {
    my ($state, $something) = @_;
    if ($something =~ /^0x[0-9a-f]+$/) {
        return hex($something);
    }
    if ($something =~ /^[0-9]+$/) {
        return $something;
    }
    die sprintf(
        "Invalid hex or decimal value: '%s' at '%s' line %d",
        $something,
        $state->{current_file},
        $state->{files}->{$state->{current_file}}->{line_count}
    );
}

sub generate_addresses {
    my ($state) = @_;
    my $addr = 0;

    # Generate addr from all the code items
    for my $code (@{$state->{prg}}) {
        $code->{addr} = $addr;
        $addr++;
        for my $arg (@{$code->{args}}) {
            $addr += $arg->{len};
        }
    }

    # Convert all the label array indexes into absolute addresses
    for my $label (keys %{$state->{markers}}) {
        my $idx = $state->{markers}->{$label};
        my $code = $state->{prg}->[$idx];
        my $label_addr = $code->{addr};
        $state->{markers}->{$label} = $label_addr;
    }

    # Resolve markers to addresses
    for my $code (@{$state->{prg}}) {
        for my $arg (@{$code->{args}}) {
            my $val = $arg->{val};
            my $len = $arg->{len};
            if (ref($val)) {
                if (exists $state->{markers}->{$val->{label}}) {
                    $arg->{val} = $state->{markers}->{$val->{label}};
                }
                else {
                    die sprintf(
                        "Unable to resolve label: '%s' at '%s' line %d",
                        $val->{label},
                        $val->{current_file},
                        $val->{line_count},
                    );
                }
            }
        }
    }
}

sub generate_binary {
    my ($state) = @_;

    my @binary;

    for my $code (@{$state->{prg}}) {
        my $addr = $code->{addr};
        $binary[$addr] = $code->{opcode};
        for my $arg (@{$code->{args}}) {
            my @codes = _expand_constant($arg->{val}, $arg->{len});
            if (scalar(@codes) != $arg->{len}) {
                die "Assembler internal error";
            }
            for (my $i = 0; $i < $arg->{len}; $i++) {
                $addr++;
                $binary[$addr] = $codes[$i];
            }
        }
    }

    return @binary;
}

sub decode_instruction {
    my ($state, $ins, @args) = @_;
    my $ucins = uc($ins);
    if (!exists $INS_ARG_LENGTH{$ucins}) {
        die sprintf(
            "Invalid instruction: '%s' at '%s' line %d",
            $ins,
            $state->{current_file},
            $state->{files}->{$state->{current_file}}->{line_count}
        );
    }

    my ($opcode, $arglens) = @{$INS_ARG_LENGTH{$ucins}};
    my $nr_args = scalar(@{$arglens});

    my $code = {
        opcode => $opcode,
        ucins  => $ucins,
        args   => [],
    };

    for (my $i = 0; $i < $nr_args; $i++) {
        my $arglen = $arglens->[$i];
        my $arg = $args[$i];
        my $val;
        if (exists $state->{defs}->{$arg}) {
            push @{$code->{args}}, {
                val => $state->{defs}->{$arg},
                len => $arglen,
            };
        }
        elsif (exists $state->{markers}->{$arg}) {
            push @{$code->{args}}, {
                val => {
                    label        => $arg,
                    current_file => $state->{current_file},
                    line_count   => $state->{files}->{$state->{current_file}}->{line_count},
                },
                len => $arglen,
            };
        }
        elsif ($arg =~ /^0x[0-9a-f]+$/) {
            push @{$code->{args}}, {
                val => _convert_to_dec($arg),
                len => $arglen,
            };
        }
        elsif ($arg =~ /^[0-9]+$/) {
            push @{$code->{args}}, {
                val => $arg,
                len => $arglen,
            };
        }
        else {
            push @{$code->{args}}, {
                val => {
                    label        => $arg,
                    current_file => $state->{current_file},
                    line_count   => $state->{files}->{$state->{current_file}}->{line_count},
                },
                len => $arglen,
            };
        }
    }

    return $code;
}

sub compile_instruction {
    my ($state, $ins, @args) = @_;
    my $code = decode_instruction($state, $ins, @args);
    push @{$state->{prg}}, $code;
}

sub compile_line {
    my ($state, $line) = @_;

    if ($line =~ /^var\s+([A-Za-z0-9\_]+)\s+([0-9a-z]+)/) {
        $state->{defs}->{$1} = _convert_to_dec($state, $2);
        return;
    }

    if ($line =~ /^([a-zA-Z\_]+):/) {
        $state->{markers}->{$1} = scalar(@{$state->{prg}}); # then next index
        return;
    }

    if ($line =~ /^\s+([^;]+)/) {
        my $command = $1;
        $command =~ s/\s+$//;
        if ($command =~ /^([a-z0-9_]+)\s*(.*)$/) {
            my ($ins, $rest) = ($1, $2);
            my @args = split(/\s+/, $rest);
            compile_instruction($state, $ins, @args);
        }
    }
}

sub load_file {
    my ($filename) = @_;
    my $program = "";
    open(my $fh, '<', $filename) || die "unable to open file $filename: $!\n";
    while (my $line = <$fh>) {
        $program .= $line;
    }
    close($fh);
    return $program;
}

sub compile_file {
    my ($state, $filename) = @_;
    my $program = load_file($filename);
    my @lines = split(/\n/, $program);
    $state->{current_file} = $filename;
    $state->{files}->{$state->{current_file}}->{line_count} = 1;
    LINE: for my $line (@lines) {
        chomp $line;
        unless ($line) {
            $state->{files}->{$state->{current_file}}->{line_count}++;
            next LINE;
        }
        compile_line($state, $line);
        $state->{files}->{$state->{current_file}}->{line_count}++;
    }
}

sub compile {
    my ($opts) = @_;

    my $state = {
        addr => 0,
        prg => [],
    };

    compile_file($state, $opts->{input});
    generate_addresses($state);

    my @binary = generate_binary($state);

    return @binary;
}

sub main {
    my ($opts) = @_;

    if ($opts->{carray}) {
        my @binary = compile($opts);
        print_carray(@binary);
    }
    elsif ($opts->{output}) {
        my @binary = compile($opts);
        write_binary($opts->{output}, @binary);
    }
    elsif ($opts->{cenum}) {
        print_cenum();
    }
    elsif ($opts->{cswitch}) {
        print_cswitch();
    }
    elsif ($opts->{copcode2str}) {
        print_copcode2str();
    }
    elsif ($opts->{ctests}) {
        print_ctests();
    }
    else {
        die "No --output=<bin> or --carray options\n";
    }
}
